{-# LANGUAGE DeriveGeneric #-}
{-# LANGUAGE TypeFamilies  #-}

-- | Definitions of the most fundamental types.

module Pos.Types.Types
       (
         NodeId (..)
       , nodeF

       , EpochIndex
       , LocalSlotIndex
       , SlotId (..)

       , Coin (..)
       , coinF

       , Address (..)
       , addressF

       , TxIn (..)
       , TxOut (..)
       , Tx (..)

       , RandomSecret (..)
       , Commitment (..)
       , Opening (..)

       , ChainDifficulty (..)
       , BlockHeader (..)
       , SignedBlockHeader (..)
       , GenericBlock (..)
       , TxsPayload
       , TxsProof (..)
       , Block
       , GenesisBlock (..)

       , Entry (..)
       , Blockkk
       , displayEntry
       ) where

import qualified Data.Text           as T (unwords)
import           Data.Text.Buildable (Buildable)
import qualified Data.Text.Buildable as Buildable
import           Data.Vector         (Vector)
import           Data.Word           (Word32, Word64)
import           Formatting          (Format, bprint, build, int, sformat, shown, (%))
import           Universum

import           Pos.Crypto          (Encrypted, Hash, PublicKey, SecretProof, Share,
                                      Signature)
import           Pos.Merkle          (MerkleRoot)
import           Pos.Util            (Raw)

----------------------------------------------------------------------------
-- Node. TODO: do we need it?
----------------------------------------------------------------------------

newtype NodeId = NodeId
    { getNodeId :: Int
    } deriving (Show, Eq, Ord, Enum)

instance Buildable NodeId where
    build = bprint ("#"%int) . getNodeId

nodeF :: Format r (NodeId -> r)
nodeF = build

----------------------------------------------------------------------------
-- Slotting
----------------------------------------------------------------------------

-- | Index of epoch.
type EpochIndex = Word64

-- | Index of slot inside a concrete epoch.
type LocalSlotIndex = Word32

-- | Slot is identified by index of epoch and local index of slot in
-- this epoch. This is a global index
data SlotId = SlotId
    { siEpoch :: !EpochIndex
    , siSlot  :: !LocalSlotIndex
    } deriving (Show, Eq, Generic)

----------------------------------------------------------------------------
-- Coin
----------------------------------------------------------------------------

-- | Coin is the least possible unit of currency.
newtype Coin = Coin
    { getCoin :: Int64
    } deriving (Num, Enum, Integral, Show, Ord, Real, Generic, Eq)

instance Buildable Coin where
    build = bprint (int%" coin(s)")

-- | Coin formatter which restricts type.
coinF :: Format r (Coin -> r)
coinF = build

----------------------------------------------------------------------------
-- Address
----------------------------------------------------------------------------

-- | Address is where you can send coins.
newtype Address = Address
    { getAddress :: PublicKey
    } deriving (Show, Eq, Generic, Buildable, Ord)

addressF :: Format r (Address -> r)
addressF = build

----------------------------------------------------------------------------
-- Transaction
----------------------------------------------------------------------------

-- | Transaction input.
data TxIn = TxIn
    { txInHash  :: !(Hash Tx)  -- ^ Which transaction's output is used
    , txInIndex :: !Word32     -- ^ Index of the output in transaction's
                               -- outputs
    } deriving (Eq, Ord, Show, Generic)

-- | Transaction output.
data TxOut = TxOut
    { txOutAddress :: !Address
    , txOutValue   :: !Coin
    } deriving (Eq, Ord, Show, Generic)

-- | Transaction.
data Tx = Tx
    { txInputs  :: ![TxIn]   -- ^ Inputs of transaction.
    , txOutputs :: ![TxOut]  -- ^ Outputs of transaction.
    } deriving (Eq, Ord, Show, Generic)

----------------------------------------------------------------------------
-- MPC
----------------------------------------------------------------------------

-- | Random secret is generated by each party at the beginning of MPC. It is also result of MPC which is used as seed for follow-the satoshi.
newtype RandomSecret = RandomSecret
    { getRandomSecret :: ByteString
    } deriving (Show, Eq, Generic)

-- | Commitment is a message generated during the first stage of
-- MPC. It contains encrypted shares and proof of secret.
data Commitment = Commitment
    { commProof  :: !SecretProof
    , commShares :: !(HashMap PublicKey (Encrypted Share))
    } deriving (Show, Eq, Generic)

-- | Opening reveals message.
-- Maybe we'll need to add something here.
newtype Opening = Opening
    { getOpening :: RandomSecret
    } deriving (Show, Eq, Generic)

----------------------------------------------------------------------------
-- Block
----------------------------------------------------------------------------

type family Proof payload :: *

newtype ChainDifficulty = ChainDifficulty
    { getChainDifficulty :: Word64
    } deriving (Show, Eq, Ord, Num)

-- | Header of block contains all the information necessary to
-- validate consensus algorithm. It also contains proof of payload
-- associated with it.
-- TODO: add MPC-types.
data BlockHeader proof = BlockHeader
    { -- | Hash of the previous block's header.
      bhPrevHash     :: !(Hash (BlockHeader proof))
    , -- | Id of the slot for which this block was generated.
      bhSlot         :: !SlotId
    , -- | Public key of slot leader.
      bhLeaderKey    :: !PublicKey
    , -- | Commitments are added during the first phase of epoch.
      bhCommitments  :: !(HashMap PublicKey Commitment)
    , -- | Openings are added during the second phase of epoch.
      bhOpenings     :: !(HashMap PublicKey Opening)
    , -- | Difficulty of chain ending in this block.
      bhDifficulty   :: !ChainDifficulty
    , -- | Proof of payload.
      bhPayloadProof :: !proof
    } deriving (Show, Eq, Generic)

-- | SignedBlockHeader consists of BlockHeader and its signature.
-- TODO: or maybe we should put public key here?
data SignedBlockHeader proof = SignedBlockHeader
    { sbhHeader    :: !(BlockHeader proof)
    , sbhSignature :: !(Signature (BlockHeader proof))
    } deriving (Show, Eq, Generic)

-- | In general Block consists of some payload and header associated
-- with it.
data GenericBlock payload = GenericBlock
    { gbHeader  :: !(SignedBlockHeader (Proof payload))
    , gbPayload :: !payload
    } deriving (Generic)

-- | In our crypto-currency payload is a list of transactions.
type TxsPayload = [Tx]

-- | Proof of transactions list.
data TxsProof = TxsProof
    { tpNumber :: !Word32          -- ^ Number of transactions.
    , tpRoot   :: !(MerkleRoot Tx) -- ^ Root of Merkle tree containing txs
    } deriving (Show, Eq, Generic)

type instance Proof TxsPayload = TxsProof

type Block = GenericBlock TxsPayload

-- | Genesis block doesn't have any payload and is not strictly
-- necessary. However, it is good idea to store list of leaders
-- somewhere instead of calculating it every time. If we decide to
-- make it part of blockchain, we will need to store hash also.
data GenesisBlock = GenesisBlock
    { gbLeaders :: !(Vector PublicKey)
    } deriving (Generic)

----------------------------------------------------------------------------
-- Block. Leftover.
----------------------------------------------------------------------------

-- | An entry in a block
data Entry

      -- | Transaction
    = ETx Tx

      -- | Hash of random string U that a node has committed to
    | EUHash NodeId (Hash Raw)
      -- | An encrypted piece of secret-shared U that the first node sent to
      -- the second node (and encrypted with the second node's pubkey)
    | EUShare NodeId NodeId (Encrypted Share)
      -- | Leaders for a specific epoch
    | ELeaders Int [NodeId]

    deriving (Eq, Ord, Show)

-- | Block
type Blockkk = [Entry]

displayEntry :: Entry -> Text
displayEntry (ETx tx) =
    "transaction " <> show tx
displayEntry (EUHash nid h) =
    sformat (nodeF%"'s commitment = "%shown) nid h
displayEntry (EUShare n_from n_to share) =
    sformat (nodeF%"'s share for "%nodeF%" = "%build) n_from n_to share
displayEntry (ELeaders epoch leaders) =
    sformat ("leaders for epoch "%int%" = "%build)
            epoch
            (T.unwords (map (toS . sformat nodeF) leaders))
